plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.21'
    id 'com.adarshr.test-logger' version '1.2.0'
    id 'net.ltgt.apt' version '0.10'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'

group = 'com.boclips'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

idea {
    module {
        testSourceDirs += file('src/client_test/kotlin')
        testResourceDirs += file('src/client_test/resources')
    }
}

sourceSets {
    client {
        java {
            srcDir 'src/client/kotlin'
        }
    }

    client_test {
        kotlin {
            compileClasspath += client.output + main.output + test.output
            runtimeClasspath += main.output + test.output

            srcDir 'src/client_test/kotlin'
        }
        resources.srcDir file('src/client_test/resources')
    }

    youtubeContractTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir 'src/test_youtube_contract/kotlin'
        }
        resources.srcDir file('src/test_youtube_contract/resources')
    }
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileYoutubeContractTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileClientKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileClient_testKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'http://repo.spring.io/libs-release/' }
    }
}

test {
    useJUnitPlatform()
}

task testYoutubeContract(type: Test) {
    testClassesDirs = sourceSets.youtubeContractTest.output.classesDirs
    classpath = sourceSets.youtubeContractTest.runtimeClasspath

    useJUnitPlatform()
}

task testClient(type: Test) {
    testClassesDirs = sourceSets.client_test.output.classesDirs
    classpath = sourceSets.client_test.runtimeClasspath

    useJUnitPlatform()
}

task buildClient(type: Jar) {
    from(sourceSets.client.output) {
        include '**/*.class'
    }
    archiveName("video-service-client-${version}.jar")
}

task sourcesJarClient(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.client.allSource
}

test.finalizedBy(testYoutubeContract)
test.finalizedBy(testClient)

def junitVersion = "5.3.1"

dependencies {
    compile project(':search-service')

    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-hateoas")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-aop")

    compile group: 'org.mongodb', name: 'mongo-java-driver', version: '3.10.1'

    compile("io.micrometer:micrometer-core")
    compile("io.micrometer:micrometer-registry-prometheus")

    compile('com.github.boclips:boclips-spring-security:0.48.0')
    compile("com.github.boclips:kaltura-client:0.49.0")

    compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8")
    compile("io.github.microutils:kotlin-logging:1.4.9")
    compile('org.simplejavamail:simple-java-mail:5.0.8')
    compile('com.google.apis:google-api-services-youtube:v3-rev206-1.25.0')
    compile('io.springfox:springfox-swagger2:2.9.2')
    compile('io.springfox:springfox-swagger-ui:2.9.2')
    compile('org.apache.commons:commons-lang3:3.7')
    compile("org.bouncycastle:bcprov-jdk15on:1.60")
    compile('org.litote.kmongo:kmongo:3.9.2')
    compile('io.sentry:sentry-logback:1.7.21')
    compile("com.fasterxml.jackson.core:jackson-databind:2.9.8") // vulnerability problem
    
    // repeated versions used in search service 
    compile('org.apache.solr:solr-solrj:7.7.1')
    compile("org.elasticsearch:elasticsearch:6.5.4")
    compile("org.elasticsearch.client:elasticsearch-rest-high-level-client:6.5.4")

    testCompile("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'junit', module: 'junit'
    }
    testCompile("org.springframework.security:spring-security-test")
    testCompile("de.flapdoodle.embed:de.flapdoodle.embed.mongo")
    testCompile("com.nhaarman:mockito-kotlin:1.5.0")
    testCompile("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testCompile("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    testCompile("org.junit.jupiter:junit-jupiter-params:$junitVersion")
    testCompile('org.yaml:snakeyaml:1.23')

    youtubeContractTestCompile sourceSets.main.output

    youtubeContractTestCompile configurations.compile
    youtubeContractTestCompile configurations.testCompile

    youtubeContractTestRuntime configurations.runtime
    youtubeContractTestRuntime configurations.testRuntime

    clientCompileOnly('org.projectlombok:lombok:1.18.4')
    clientApt('org.projectlombok:lombok:1.18.4')
    clientCompileOnly('org.springframework.security.oauth:spring-security-oauth2:2.1.3.RELEASE')
    clientCompileOnly("org.springframework.boot:spring-boot-starter-test")
    client_testCompile('org.springframework.security.oauth:spring-security-oauth2:2.1.3.RELEASE')
    clientCompileOnly("org.springframework.boot:spring-boot-actuator")
    client_testCompile("org.springframework.boot:spring-boot-actuator")

    testCompile("com.github.tomakehurst:wiremock:2.19.0") {
        exclude group: 'junit', module: 'junit'
    }

    client_testCompile sourceSets.main.output
    client_testCompile sourceSets.test.output
    client_testCompile sourceSets.client.output

    client_testCompile configurations.compile
    client_testCompile configurations.testCompile
    client_testCompile configurations.clientCompile

    client_testRuntime configurations.runtime
    client_testRuntime configurations.testRuntime
    client_testRuntime configurations.clientRuntime
}
