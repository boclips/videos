plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.61'
    id 'com.adarshr.test-logger' version '1.2.0'
    id 'net.ltgt.apt' version '0.10'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'
apply plugin: 'kotlin'

group = 'com.boclips'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()

    flatDir {
        dirs 'libs'
    }
}

sourceSets {
    youtubeContractTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir 'src/test_youtube_contract/kotlin'
        }
        resources.srcDir file('src/test_youtube_contract/resources')
    }
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileYoutubeContractTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'http://repo.spring.io/libs-release/' }
    }
}

test {
    useJUnitPlatform()
}

task testYoutubeContract(type: Test) {
    testClassesDirs = sourceSets.youtubeContractTest.output.classesDirs
    classpath = sourceSets.youtubeContractTest.runtimeClasspath

    useJUnitPlatform()
}

test.finalizedBy(testYoutubeContract)

ext["junit-jupiter.version"] = junitVersion
ext["elasticsearch.version"] = elasticSearchVersion
ext["solr.version"] = solrVersion

dependencies {
    implementation project(':video-service-api')
    implementation project(':search-service')

    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-hateoas")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation('org.springframework.retry:spring-retry')

    implementation('org.springframework.security.oauth:spring-security-oauth2:2.4.0.RELEASE')

    implementation("org.mongodb:mongo-java-driver:3.10.1")

    implementation("io.micrometer:micrometer-core")
    implementation("io.micrometer:micrometer-registry-prometheus")

    implementation('com.github.boclips:boclips-spring-web:0.128.0')
    implementation('com.github.boclips:boclips-spring-security:0.412.0')
    implementation("com.github.boclips:event-bus:4.2.16")
    implementation('com.github.boclips:kaltura-client:2.0.2')
    implementation("com.github.boclips:user-service:v224")

    // Required by user-service client
    implementation('io.github.openfeign:feign-core:10.7.0')

    implementation("io.github.microutils:kotlin-logging:1.4.9")
    implementation('com.google.apis:google-api-services-youtube:v3-rev206-1.25.0')
    implementation('com.google.cloud:google-cloud-storage:1.103.1')
    implementation('org.apache.commons:commons-lang3:3.7')
    implementation("org.bouncycastle:bcprov-jdk15on:1.60")
    implementation('org.litote.kmongo:kmongo:3.9.2')
    implementation('io.sentry:sentry-logback:1.7.21')
    implementation("io.opentracing.contrib:opentracing-spring-jaeger-web-starter:2.0.3")
    implementation("io.opentracing.contrib:opentracing-mongo-driver:0.1.5")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.springframework.security:spring-security-test")
    testImplementation("de.flapdoodle.embed:de.flapdoodle.embed.mongo")
    testImplementation('com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0')
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation('org.mockito:mockito-junit-jupiter:2.23.0')
    testImplementation('org.yaml:snakeyaml:1.23')

    youtubeContractTestImplementation sourceSets.main.output
    youtubeContractTestImplementation configurations.compile
    youtubeContractTestImplementation configurations.testImplementation
    youtubeContractTestRuntime configurations.runtime
    youtubeContractTestRuntime configurations.testRuntime

    testImplementation("com.github.tomakehurst:wiremock:2.19.0") {
        exclude group: 'junit', module: 'junit'
    }
    testImplementation("com.damnhandy:handy-uri-templates:2.1.8")
}
